#!Nsis Installer Command Script
#
# The following external defines are recognized:
# ${VERSION} = YYYYMMDD
# ${BIT}     = 32 or 64
# ${LIBGCC}  = name of libgcc dll file to use
# ${MINGW}   = <path to mingw windows dlls>
# ${MPI}     = 1 when MPI is compiled in

!include "LogicLib.nsh"
!include "EnvVarUpdate.nsh"
!include "x64.nsh"

!ifndef ${LIBGCC}
!define LIBGCC libgcc_s_sjlj-1.dll
!endif

!ifdef ${MPI}
!define LAMMPS "LAMMPS ${BIT}-bit MPI ${VERSION}"
OutFile "lammps-${BIT}bit-mpich2-${VERSION}.exe"
!else
!define LAMMPS "LAMMPS ${BIT}-bit ${VERSION}"
OutFile "lammps-${BIT}bit-${VERSION}.exe"
!endif

Name "${LAMMPS}"
!if ${BIT} == 64
InstallDir "$ProgramFiles64\${LAMMPS}"
!else
InstallDir "$ProgramFiles\${LAMMPS}"
!endif

XPStyle on
ShowInstDetails show
ShowUninstDetails show

Page directory
Page instfiles

DirText "Please select the LAMMPS installation folder."

Section "${LAMMPS}"
  SectionIn RO
  SetRegView ${BIT}

  SetOutPath "$INSTDIR"
  CreateDirectory "$INSTDIR\Doc"
  CreateDirectory "$INSTDIR\Benchmarks"
  CreateDirectory "$INSTDIR\Examples"
  CreateDirectory "$INSTDIR\Potentials"
  CreateDirectory "$SMPROGRAMS\${LAMMPS}"

  File ${MINGW}/${LIBGCC}
  File ${MINGW}/libgfortran-3.dll
  File ${MINGW}/libgomp-1.dll
  File ${MINGW}/libjpeg-62.dll
  File ${MINGW}/libquadmath-0.dll
  File ${MINGW}/libstdc++-6.dll
  File ${MINGW}/pthreadGC2.dll

!ifdef ${MPI}
  File ${BIT}-mpi/lmp_mpi.exe
  File /x lmp_serial.exe ${BIT}bit/*.exe
!else
  File ${BIT}bit/*.exe
!endif
  File ${BIT}bit/*.dll
  File /oname=License.txt        lammps-current/LICENSE
  File /oname=README.txt         lammps-current/README

  SetOutPath "$INSTDIR\Doc"
  File /oname=LAMMPS-Manual.pdf  lammps-current/doc/Manual.pdf
  File /oname=Colvars-Manual.pdf lammps-current/doc/PDF/colvars-refman-lammps.pdf

  SetOutPath "$INSTDIR\Potentials"
  File lammps-current/potentials/*

  SetOutPath "$INSTDIR\Examples"
  File /r lammps-current/examples/*

  SetOutPath "$INSTDIR\Benchmarks"
  File /r lammps-current/bench/*

  CreateShortCut "$SMPROGRAMS\${LAMMPS}\Uninstall.lnk"      "$INSTDIR\Uninstall.exe"          "" "$INSTDIR\Uninstall.exe"          0
  CreateShortCut "$SMPROGRAMS\${LAMMPS}\README.lnk"         "$INSTDIR\README.txt"             "" "$INSTDIR\README.txt"             0
  CreateShortCut "$SMPROGRAMS\${LAMMPS}\LAMMPS Manual.lnk"  "$INSTDIR\doc\LAMMPS-Manual.pdf"  "" "$INSTDIR\doc\LAMMPS-Manual.pdf"  0
  CreateShortCut "$SMPROGRAMS\${LAMMPS}\Colvars Manual.lnk" "$INSTDIR\doc\Colvars-Manual.pdf" "" "$INSTDIR\doc\Colvars-Manual.pdf" 0

  ${EnvVarUpdate} $0 "PATH" "A" "HKLM" "$INSTDIR"
  ${EnvVarUpdate} $0 "LAMMPS_POTENTIALS" "A" "HKLM" "$INSTDIR\Potentials"
SectionEnd

Section "Uninstall"
  SetRegView ${BIT}

  ${un.EnvVarUpdate} $0 "PATH" "R" "HKLM" "$INSTDIR"
  ${un.EnvVarUpdate} $0 "LAMMPS_POTENTIALS" "R" "HKLM" "$INSTDIR\Potentials"

  Delete /REBOOTOK "$SMPROGRAMS\${LAMMPS}\*.lnk"
  RMDir  /REBOOTOK "$SMPROGRAMS\${LAMMPS}"

  Delete /REBOOTOK "$INSTDIR\*.exe"
  Delete /REBOOTOK "$INSTDIR\*.dll"
  Delete /REBOOTOK "$INSTDIR\*.txt"
  RMDir /r /REBOOTOK "$INSTDIR\Benchmarks"
  RMDir /r /REBOOTOK "$INSTDIR\Doc"
  RMDir /r /REBOOTOK "$INSTDIR\Examples"
  RMDir /r /REBOOTOK "$INSTDIR\Potentials"
  RMDir /REBOOTOK "$INSTDIR"
SectionEnd

Section -post
  SetRegView ${BIT}
  WriteUninstaller "$INSTDIR\Uninstall.exe"
SectionEnd

# Local Variables:
# mode: sh
# End:

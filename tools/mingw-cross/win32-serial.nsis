#!Nsis Installer Command Script
#
# This is an NSIS Installer Command Script
# To build an installer from the script you would normally do:
#
#   makensis this_script

!include "LogicLib.nsh"
!include "EnvVarUpdate.nsh"

Name "LAMMPS 32-bit @VERSION@"
OutFile "lammps-32bit-@VERSION@.exe"
InstallDir "$ProgramFiles\LAMMPS 32-bit @VERSION@"

XPStyle on
ShowInstDetails show
ShowUninstDetails show

Page directory
Page instfiles

DirText "Please select the LAMMPS installation folder."

Section "LAMMPS 32-bit @VERSION@"
  SectionIn RO

  SetOutPath "$INSTDIR"
  CreateDirectory "$INSTDIR\Doc"
  CreateDirectory "$INSTDIR\Benchmarks"
  CreateDirectory "$INSTDIR\Examples"
  CreateDirectory "$INSTDIR\Potentials"

  File /usr/i686-w64-mingw32/sys-root/mingw/bin/libgcc_s_sjlj-1.dll
  File /usr/i686-w64-mingw32/sys-root/mingw/bin/libgfortran-3.dll
  File /usr/i686-w64-mingw32/sys-root/mingw/bin/libgomp-1.dll
  File /usr/i686-w64-mingw32/sys-root/mingw/bin/libjpeg-62.dll
  File /usr/i686-w64-mingw32/sys-root/mingw/bin/libquadmath-0.dll
  File /usr/i686-w64-mingw32/sys-root/mingw/bin/libstdc++-6.dll
  File /usr/i686-w64-mingw32/sys-root/mingw/bin/pthreadGC2.dll

  File 32bit/*.exe
  File 32bit/*.dll
  File /oname=License.txt        lammps-current/LICENSE
  File /oname=README.txt         lammps-current/README

  SetOutPath "$INSTDIR\Doc"
  File /oname=LAMMPS-Manual.pdf  lammps-current/doc/Manual.pdf
  File /oname=Colvars-Manual.pdf lammps-current/doc/PDF/colvars-refman-lammps.pdf

  SetOutPath "$INSTDIR\Potentials"
  File lammps-current/potentials/*

  SetOutPath "$INSTDIR\Examples"
  File /r lammps-current/examples/*

  SetOutPath "$INSTDIR\Benchmarks"
  File /r lammps-current/bench/*

SectionEnd

Section "Start Menu Shortcuts"
  CreateDirectory "$SMPROGRAMS\LAMMPS 32-bit @VERSION@"
  CreateShortCut "$SMPROGRAMS\LAMMPS 32-bit @VERSION@\Uninstall.lnk"      "$INSTDIR\Uninstall.exe"          "" "$INSTDIR\Uninstall.exe"          0
  CreateShortCut "$SMPROGRAMS\LAMMPS 32-bit @VERSION@\README.lnk"         "$INSTDIR\README.txt"             "" "$INSTDIR\README.txt"             0
  CreateShortCut "$SMPROGRAMS\LAMMPS 32-bit @VERSION@\LAMMPS Manual.lnk"  "$INSTDIR\doc\LAMMPS-Manual.pdf"  "" "$INSTDIR\doc\LAMMPS-Manual.pdf"  0
  CreateShortCut "$SMPROGRAMS\LAMMPS 32-bit @VERSION@\Colvars Manual.lnk" "$INSTDIR\doc\Colvars-Manual.pdf" "" "$INSTDIR\doc\Colvars-Manual.pdf" 0
SectionEnd

Section "Updating Environment"
  ${EnvVarUpdate} $0 "PATH" "A" "HKLM" "$INSTDIR"
  ${EnvVarUpdate} $0 "LAMMPS_POTENTIALS" "A" "HKLM" "$INSTDIR\Potentials"
SectionEnd

Section "Uninstall"
  ${un.EnvVarUpdate} $0 "PATH" "R" "HKLM" "$INSTDIR"
  ${un.EnvVarUpdate} $0 "LAMMPS_POTENTIALS" "R" "HKLM" "$INSTDIR\Potentials"

  Delete /REBOOTOK "$SMPROGRAMS\LAMMPS 32-bit @VERSION@\*.lnk"
  RMDir  /REBOOTOK "$SMPROGRAMS\LAMMPS 32-bit @VERSION@"

  Delete /REBOOTOK "$INSTDIR\*.exe"
  Delete /REBOOTOK "$INSTDIR\*.dll"
  Delete /REBOOTOK "$INSTDIR\*.txt"
  RMDir /r /REBOOTOK "$INSTDIR\Benchmarks"
  RMDir /r /REBOOTOK "$INSTDIR\Doc"
  RMDir /r /REBOOTOK "$INSTDIR\Examples"
  RMDir /r /REBOOTOK "$INSTDIR\Potentials"
  RMDir /REBOOTOK "$INSTDIR"

  DeleteRegKey HKLM SOFTWARE\LAMMPS-32bit-@VERSION@
SectionEnd

Section -post
  WriteUninstaller "$INSTDIR\Uninstall.exe"
SectionEnd

# Local Variables:
# mode: sh
# End:
